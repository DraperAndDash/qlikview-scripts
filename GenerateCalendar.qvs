SUB GenerateCalendar(Region, Date, Table)

  IF SubField(OSUser(),'\',1) = 'DRAPERANDDASH' THEN

    SET vCalendarPath = 'http://discover2/opendata/qlikview-scripts/MasterCalendar.html';

  ELSE

    SET vCalendarPath = 'http://discover.draperanddash.com/opendata/qlikview-scripts/MasterCalendar.html';

  ENDIF

  $(Table)DateRange:
  LOAD min($(Date)) as minDate,
     max($(Date)) as maxDate
  Resident $(Table);
  
  LET vMinDate = Num(Peek('minDate',0,'$(Table)DateRange'));
  LET vMaxDate = Num(Peek('maxDate',0,'$(Table)DateRange'));
  
  DROP Table $(Table)DateRange;
  
  QUALIFY *;
  UNQUALIFY CalendarDate, $(Date);
  
  $(Date)_Cal:
  LOAD Date(CalendarDate,'DD/MM/YYYY') as $(Date),
       $(Region)_Year_FY as Year_FY, 
       Year_CY, 
       Month, 
       MonthName, 
       Day, 
       $(Region)_Quarter_FY as Quarter_FY, 
       Quarter_CY, 
       $(Region)_MonthNumber_FY as MonthNumber_FY,  
       MonthNumber_CY, 
       $(Region)_QuarterName_FY as QuarterName_FY, 
       QuarterName_CY, 
       $(Region)_WeekYear_FY as WeekYear_FY, 
       WeekYear_CY, 
       WeekDay, 
       [w/e Date], 
       [$(Region)_Week Number_FY] as [Week Number_FY],  
       [Week Number_CY], 
       WeekendFlag
  FROM
  [$(vCalendarPath)]
  (html, codepage is 1252, embedded labels, table is @1)
  Where CalendarDate >= $(vMinDate) and CalendarDate <= $(vMaxDate);
    
  UNQUALIFY *;

  LET vMaxMonth = '=MonthName(Max('&chr(36)&'(=GetFIeldSelections(Calendar)&'&chr(39)&'_Cal.Month'&chr(39)&')))';
  LET vPreviousMonth = '=MonthName(Max('&chr(36)&'(=GetFIeldSelections(Calendar)&'&chr(39)&'_Cal.Month'&chr(39)&'),2))';
  LET vMaxQuarter = '=MaxString('&chr(36)&'(=GetFIeldSelections(Calendar)&'&chr(39)&'_Cal.Quarter_'&chr(39)&'&vCal))';
  LET vPreviousQuarter = '=MaxString({<'&chr(36)&'(=GetFieldSelections(Calendar)&'&chr(39)&'_Cal.Month'&chr(39)&')={'&chr(39)&chr(36)&'(=MonthName(AddMonths(vMaxMonth,-3)))'&chr(39)&'}>} '&chr(36)&'(=GetFieldSelections(Calendar)&'&chr(39)&'_Cal.Quarter_'&chr(39)&'&vCal))';
  LET vMaxYear = '=MaxString('&chr(36)&'(=GetFIeldSelections(Calendar)&'&chr(39)&'_Cal.Year_'&chr(39)&'&vCal))';;
  LET vPreviousYear = '=MaxString({<'&chr(36)&'(=GetFieldSelections(Calendar)&'&chr(39)&'_Cal.Month'&chr(39)&')={'&chr(39)&chr(36)&'(=MonthName(AddMonths(vMaxMonth,-12)))'&chr(39)&'}>} '&chr(36)&'(=GetFieldSelections(Calendar)&'&chr(39)&'_Cal.Year_'&chr(39)&'&vCal))';
  LET vMaxWeek = '=Date(Max('&chr(36)&'(='&chr(39)&'['&chr(39)&'&GetFIeldSelections(Calendar)&'&chr(39)&'_Cal.w/e Date]'&chr(39)&')),'&chr(39)&'DD/MM/YYYY'&chr(39)&')';
  LET vPreviousWeek = '=Date(Max('&chr(36)&'(='&chr(39)&'['&chr(39)&'&GetFIeldSelections(Calendar)&'&chr(39)&'_Cal.w/e Date]'&chr(39)&')-7),'&chr(39)&'DD/MM/YYYY'&chr(39)&')';

ENDSUB